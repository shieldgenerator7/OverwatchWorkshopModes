settings
{
	lobby
	{
		Allow Players Who Are In Queue: Yes
		Max FFA Players: 6
		Max Spectators: 11
	}

	modes
	{
		disabled Assault
		{
			Limit Roles: 2 Of Each Role Per Team
		}

		disabled Control
		{
			Limit Roles: 2 Of Each Role Per Team
		}

		Deathmatch
		{
			enabled maps
			{
				Workshop Expanse
			}
		}

		disabled Escort
		{
			Limit Roles: 2 Of Each Role Per Team
		}

		disabled Hybrid
		{
			Limit Roles: 2 Of Each Role Per Team
		}
	}

	heroes
	{
		General
		{
			Lúcio
			{
				Amp It Up Cooldown Time: 0%
				Movement Speed: 200%
				Soundwave Cooldown Time: 0%
			}

			McCree
			{
				Combat Roll Cooldown Time: 50%
				Flashbang Cooldown Time: 50%
				Health: 25%
				Movement Speed: 150%
				No Ammunition Requirement: On
				Secondary Fire: Off
				Ultimate Generation - Combat Deadeye: 200%
				Ultimate Generation - Passive Deadeye: 200%
				Ultimate Generation Deadeye: 200%
			}

			Orisa
			{
				Movement Speed: 200%
			}

			enabled heroes
			{
				Lúcio
				McCree
				Pharah
			}
		}
	}
}

variables
{
	global:
		0: loopVar
		1: MOUNT_RADIUS
		2: RIDER_OFFSET
		3: HORSE_FLEE_RADIUS
		4: HORSE_LOOK_TURN_ANGLE

	player:
		0: rider
		1: horse
		2: facingDirection
		3: fleeUntarget
		4: fleeDestination
}

subroutines
{
	1: horseGetDismount
}

rule("Game Setup (Spawn Orisa)")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		"Loop last slots"
		For Global Variable(loopVar, 0, 12, 1);
			"Spawn Orisas"
			Create Dummy Bot(Hero(Orisa), All Teams, Global Variable(loopVar), Vector(Random Integer(-20.000, 20), 0, Random Integer(-20.000,
				20)), Vector(0, 1, 0));
		End;
		Set Global Variable(MOUNT_RADIUS, 2);
		Set Global Variable(RIDER_OFFSET, Up);
		Set Global Variable(HORSE_FLEE_RADIUS, 20);
		Set Global Variable(HORSE_LOOK_TURN_ANGLE, 90);
	}
}

rule("==TEST== SKIP SETUP ==")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is In Setup == True;
	}

	actions
	{
		Set Match Time(0);
	}
}

rule("==TEST== SKIP ASSEMBLY ==")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Assembling Heroes == True;
	}

	actions
	{
		Set Match Time(0);
	}
}

rule("==TEST== Reload kills player ==")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Reload) == True;
	}

	actions
	{
		Kill(Event Player, Null);
	}
}

rule("Pharah Pointer")
{
	event
	{
		Ongoing - Each Player;
		All;
		Pharah;
	}

	actions
	{
		Create HUD Text(Event Player, Custom String("Position: {0}", Position Of(Event Player), Null, Null), Null, Null, Left, 0, White,
			White, White, String, Default Visibility);
	}
}

rule("Horse (Lonely) finds a Rider (Lonely)")
{
	event
	{
		Ongoing - Each Player;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Is Alive(Event Player) == True;
		Player Variable(Event Player, rider) == Null;
		"If there's a rider nearby that is alive and lonely"
		Is True For Any(Players Within Radius(Position Of(Event Player), Global Variable(MOUNT_RADIUS), All Teams, Off), And(And(Compare(
			Hero Of(Current Array Element), !=, Hero(Orisa)), Compare(Has Status(Current Array Element, Knocked Down), ==, False)), And(
			Compare(Is Alive(Current Array Element), ==, True), Compare(Player Variable(Current Array Element, horse), ==, Null))))
			== True;
	}

	actions
	{
		"Set rider"
		Set Player Variable(Event Player, rider, Value In Array(Filtered Array(Players Within Radius(Position Of(Event Player),
			Global Variable(MOUNT_RADIUS), All Teams, Off), And(And(Compare(Hero Of(Current Array Element), !=, Hero(Orisa)), Compare(
			Has Status(Current Array Element, Knocked Down), ==, False)), And(Compare(Is Alive(Current Array Element), ==, True), Compare(
			Player Variable(Current Array Element, horse), ==, Null)))), 0));
		"Set rider's horse to this"
		Set Player Variable(Player Variable(Event Player, rider), horse, Event Player);
		"Heal horse"
		Heal(Event Player, Player Variable(Event Player, rider), 1000);
		"Heal rider"
		Heal(Player Variable(Event Player, rider), Event Player, 1000);
		Set Status(Player Variable(Event Player, rider), Event Player, Phased Out, 9999);
	}
}

rule("Rider rides horse (actions copied from \\\"Stacks\\\" by Squ1dward)")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Hero Of(Event Player) != Hero(Orisa);
		Is Alive(Event Player) == True;
		Player Variable(Event Player, horse) != Null;
	}

	actions
	{
		Apply Impulse(Event Player, Direction Towards(Position Of(Event Player), Add(Position Of(Event Player), Vector(
			Speed Of In Direction(Event Player, Left), Vertical Speed Of(Event Player), Speed Of In Direction(Event Player, Forward)))),
			Multiply(Speed Of(Event Player), -0.800), To World, Incorporate Contrary Motion);
		Apply Impulse(Event Player, Direction Towards(Event Player, Add(Position Of(Player Variable(Event Player, horse)), Global Variable(
			RIDER_OFFSET))), Multiply(Distance Between(Position Of(Event Player), Add(Position Of(Player Variable(Event Player, horse)),
			Global Variable(RIDER_OFFSET))), 10), To World, Cancel Contrary Motion);
		Skip If(Or(Compare(Distance Between(Position Of(Event Player), Position Of(Player Variable(Event Player, horse))), <, 2), Compare(
			Is Alive(Player Variable(Event Player, horse)), !=, True)), 3);
		Set Player Variable(Event Player, facingDirection, Vertical Facing Angle Of(Event Player));
		Teleport(Event Player, Add(Position Of(Player Variable(Event Player, horse)), Global Variable(RIDER_OFFSET)));
		Set Facing(Event Player, Direction From Angles(Horizontal Facing Angle Of(Event Player), Player Variable(Event Player,
			facingDirection)), To World);
		Wait(0.016, Ignore Condition);
		Loop If Condition Is True;
	}
}

rule("Horse at low health, eject rider")
{
	event
	{
		Player Took Damage;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Normalized Health(Event Player) <= 0.300;
		Player Variable(Event Player, rider) != Null;
		Attacker != Player Variable(Event Player, rider);
	}

	actions
	{
		Call Subroutine(horseGetDismount);
	}
}

rule("Horse follow direction of rider")
{
	event
	{
		Ongoing - Each Player;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Is Alive(Event Player) == True;
		Player Variable(Event Player, rider) != Null;
	}

	actions
	{
		Start Facing(Event Player, Facing Direction Of(Player Variable(Event Player, rider)), 75, To World, Direction and Turn Rate);
		Start Forcing Throttle(Event Player, 1, 1, 0, 1, 0, 1);
	}
}

rule("Horse Get Dismounted")
{
	event
	{
		Subroutine;
		horseGetDismount;
	}

	actions
	{
		Apply Impulse(Player Variable(Event Player, rider), Multiply(2, Add(Up, Multiply(Facing Direction Of(Event Player), -1.000))), 10,
			To World, Cancel Contrary Motion);
		Clear Status(Player Variable(Event Player, rider), Phased Out);
		Set Status(Player Variable(Event Player, rider), Attacker, Knocked Down, 3);
		Set Player Variable(Player Variable(Event Player, rider), horse, Null);
		Set Player Variable(Event Player, rider, Null);
		Stop Facing(Event Player);
		Stop Forcing Throttle(Event Player);
	}
}

rule("Horse Died")
{
	event
	{
		Player Died;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Player Variable(Event Player, rider) != Null;
	}

	actions
	{
		Call Subroutine(horseGetDismount);
	}
}

rule("Rider died")
{
	event
	{
		Player Died;
		All;
		All;
	}

	conditions
	{
		Hero Of(Event Player) != Hero(Orisa);
		Player Variable(Event Player, horse) != Null;
	}

	actions
	{
		Stop Facing(Player Variable(Event Player, horse));
		Stop Forcing Throttle(Player Variable(Event Player, horse));
		Set Player Variable(Player Variable(Event Player, rider), horse, Null);
		Set Player Variable(Event Player, rider, Null);
		Clear Status(Event Player, Phased Out);
	}
}

rule("Rider harms horse on accident")
{
	event
	{
		Player Dealt Damage;
		All;
		All;
	}

	conditions
	{
		Hero Of(Event Player) != Hero(Orisa);
		Player Variable(Event Player, horse) != Null;
		Victim == Player Variable(Event Player, horse);
	}

	actions
	{
		Heal(Player Variable(Event Player, horse), Event Player, Multiply(Event Damage, 1.500));
	}
}

rule("AI Orisa (Lonely) Run from Rider (Lonely)")
{
	event
	{
		Ongoing - Each Player;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Is Alive(Event Player) == True;
		Player Variable(Event Player, rider) == Null;
		Is Dummy Bot(Event Player) == True;
		"If there's a rider nearby that is alive and lonely"
		Is True For Any(Players in View Angle(Event Player, All Teams, 45), And(And(Compare(Hero Of(Current Array Element), !=, Hero(
			Orisa)), Compare(Has Status(Current Array Element, Knocked Down), ==, False)), And(Compare(Is Alive(Current Array Element), ==,
			True), Compare(Player Variable(Current Array Element, horse), ==, Null)))) == True;
	}

	actions
	{
		"Set rider"
		Set Player Variable(Event Player, fleeUntarget, Value In Array(Filtered Array(Players in View Angle(Event Player, All Teams, 45),
			And(And(Compare(Hero Of(Current Array Element), !=, Hero(Orisa)), Compare(Has Status(Current Array Element, Knocked Down), ==,
			False)), And(Compare(Is Alive(Current Array Element), ==, True), Compare(Player Variable(Current Array Element, horse), ==,
			Null)))), 0));
		Set Player Variable(Event Player, fleeDestination, Nearest Walkable Position(Multiply(Subtract(Position Of(Event Player),
			Position Of(Player Variable(Event Player, fleeUntarget))), Global Variable(HORSE_FLEE_RADIUS))));
		Start Facing(Event Player, Subtract(Player Variable(Event Player, fleeDestination), Position Of(Event Player)), 100, To World,
			Direction and Turn Rate);
		Start Forcing Throttle(Event Player, 1, 1, 0, 1, 0, 1);
	}
}

rule("AI Orisa die - reset fleeDestination")
{
	event
	{
		Player Died;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Is Dummy Bot(Event Player) == True;
	}

	actions
	{
		Set Player Variable(Event Player, fleeDestination, Vector(0, 0, 0));
	}
}

rule("AI Orisa (Lonely) stop fleeing")
{
	event
	{
		Ongoing - Each Player;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Is Alive(Event Player) == True;
		Player Variable(Event Player, rider) == Null;
		Is Dummy Bot(Event Player) == True;
		"If there's a rider nearby that is alive and lonely"
		Is True For Any(Players Within Radius(Position Of(Event Player), Global Variable(HORSE_FLEE_RADIUS), All Teams, Surfaces), And(And(
			Compare(Hero Of(Current Array Element), !=, Hero(Orisa)), Compare(Has Status(Current Array Element, Knocked Down), ==, False)),
			And(Compare(Is Alive(Current Array Element), ==, True), Compare(Player Variable(Current Array Element, horse), ==, Null))))
			== False;
	}

	actions
	{
		Stop Forcing Throttle(Event Player);
		Stop Facing(Event Player);
	}
}

rule("AI Orisa (Lonely) turn around occassionally")
{
	event
	{
		Ongoing - Each Player;
		All;
		Orisa;
	}

	conditions
	{
		Hero Of(Event Player) == Hero(Orisa);
		Is Alive(Event Player) == True;
		Player Variable(Event Player, rider) == Null;
		Is Dummy Bot(Event Player) == True;
		"Horse is standing still"
		Throttle Of(Event Player) == Vector(0, 0, 0);
		"If there's a rider nearby that is alive and lonely"
		disabled Is True For Any(Players in View Angle(Event Player, All Teams, 45), And(And(Compare(Hero Of(Current Array Element), !=, Hero(
			Orisa)), Compare(Has Status(Current Array Element, Knocked Down), ==, False)), And(Compare(Is Alive(Current Array Element), ==,
			True), Compare(Player Variable(Current Array Element, horse), ==, Null)))) == True;
	}

	actions
	{
		"Orisa change direction"
		Start Facing(Event Player, Direction From Angles(Add(Random Integer(Multiply(Global Variable(HORSE_LOOK_TURN_ANGLE), -1.000),
			Global Variable(HORSE_LOOK_TURN_ANGLE)), Horizontal Angle From Direction(Facing Direction Of(Event Player))), 0), 100,
			To World, Direction and Turn Rate);
		Wait(Random Integer(0, 5), Ignore Condition);
		Loop If Condition Is True;
	}
}