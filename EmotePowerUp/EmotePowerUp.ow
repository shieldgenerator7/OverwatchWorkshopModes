rule("Player spawn in, set up")
{
	event
	{
		Player Joined Match;
		All;
		All;
	}

	actions
	{
		Set Player Variable(Event Player, P, 100);
		Create HUD Text(Event Player, String("{0}: {1}", String("Power", Null, Null, Null), Player Variable(Event Player, P), Null), Null,
			Null, Left, 0, White, White, White, Visible To and String);
	}
}

rule("Emote to Gain Power")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Communicating Any Emote(Event Player) == True;
	}

	actions
	{
		Modify Player Variable(Event Player, P, Add, 1);
		Wait(0.100, Ignore Condition);
		Loop If Condition Is True;
	}
}

rule("If P != Q, adjust values for new power level")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Player Variable(Event Player, P) != Player Variable(Event Player, Q);
	}

	actions
	{
		Set Player Variable(Event Player, Q, Player Variable(Event Player, P));
		Set Damage Dealt(Event Player, Player Variable(Event Player, P));
		Set Move Speed(Event Player, Max(100, Divide(Player Variable(Event Player, P), 2)));
		Set Healing Received(Event Player, Player Variable(Event Player, P));
	}
}

rule("Slowly lose power")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Player Variable(Event Player, P) > 100;
		Is Communicating Any Emote(Event Player) == False;
	}

	actions
	{
		Modify Player Variable(Event Player, P, Subtract, Divide(Player Variable(Event Player, P), 100));
		Skip If(Compare(Player Variable(Event Player, P), <, 500), 1);
		Modify Player Variable(Event Player, P, Subtract, Divide(Player Variable(Event Player, P), 100));
		Wait(0.500, Ignore Condition);
		Loop If Condition Is True;
	}
}

rule("Lose power with each hit")
{
	event
	{
		Player Dealt Damage;
		All;
		All;
	}

	conditions
	{
		Player Variable(Event Player, P) > 100;
	}

	actions
	{
		Modify Player Variable(Event Player, P, Subtract, Divide(Event Damage, 100));
	}
}

rule("Reset Power when die")
{
	event
	{
		Player Died;
		All;
		All;
	}

	actions
	{
		Set Player Variable(Event Player, P, 100);
	}
}

rule("Update max health at certain values")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Modulo(Player Variable(Event Player, P), 50) == 0;
	}

	actions
	{
		disabled Set Max Health(Event Player, Player Variable(Event Player, P));
		disabled Wait(0.250, Ignore Condition);
		disabled Heal(Event Player, Event Player, 25);
		Stop Heal Over Time(Player Variable(Event Player, H));
		Start Heal Over Time(Event Player, Event Player, 9999, Divide(Player Variable(Event Player, P), 100));
		Set Player Variable(Event Player, H, Last Heal Over Time ID);
	}
}

rule("== Made by ShieldGnrtr7 == Last updated 2019-09-15 20:13 ==")
{
	event
	{
		Ongoing - Global;
	}
}