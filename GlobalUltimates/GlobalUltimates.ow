variables
{
	global:
		0: GLOBAL_ULT_DURATION
		1: ASHE_ULT_DURATION
		2: asheUltTimeLeft
}

rule("Game Setup")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.GLOBAL_ULT_DURATION = 10;
		Global.ASHE_ULT_DURATION = 8;
	}
}

rule("Skip setup")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is In Setup == True;
	}

	actions
	{
		Set Match Time(0);
	}
}

rule("Skip assembly")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Assembling Heroes == True;
	}

	actions
	{
		Set Match Time(0);
	}
}

rule("Player Setup - Disable ultimate")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Alive(Event Player) == True;
	}

	actions
	{
		Disallow Button(Event Player, Button(Ultimate));
	}
}

rule("Player presses ultimate button")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Ultimate)) == True;
		Ultimate Charge Percent(Event Player) == 100;
	}

	actions
	{
		Set Ultimate Charge(Event Player, 0);
	}
}

rule("ULT: Ashe")
{
	event
	{
		Ongoing - Each Player;
		All;
		Ashe;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Ultimate)) == True;
		Ultimate Charge Percent(Event Player) == 100;
		Hero Of(Event Player) == Hero(Ashe);
	}

	actions
	{
		Big Message(All Players(All Teams), Custom String("{0} HANDSOME FOR 8 SECONDS", Hero Icon String(Hero(Ashe))));
		Global.asheUltTimeLeft = Global.ASHE_ULT_DURATION;
		Chase Global Variable Over Time(asheUltTimeLeft, 0, Global.ASHE_ULT_DURATION, None);
	}
}

rule("ULT: Ashe effect")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Global.asheUltTimeLeft > 0;
	}

	actions
	{
		disabled Big Message(All Players(All Teams), Custom String("HANDSOME FOR {0} SECONDS", Global.asheUltTimeLeft));
		Communicate(Filtered Array(All Players(All Teams), Is Communicating Any Emote(Current Array Element) == False && Hero Of(
			Current Array Element) != Hero(Ashe)), Emote Up);
		Wait(0.500, Ignore Condition);
		Loop If Condition Is True;
		Big Message(All Players(All Teams), Custom String("{0} OK BACK TO CASUAL", Hero Icon String(Hero(Ashe))));
	}
}

rule("ULT: Reinhardt")
{
	event
	{
		Ongoing - Each Player;
		All;
		Reinhardt;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Ultimate)) == True;
		Ultimate Charge Percent(Event Player) == 100;
		Hero Of(Event Player) == Hero(Reinhardt);
	}

	actions
	{
		Big Message(All Players(All Teams), Custom String("{0} EARTHQUAKE!!!", Hero Icon String(Hero(Reinhardt))));
		Set Status(Filtered Array(All Players(All Teams), Is On Ground(Current Array Element) == True && Hero Of(Current Array Element)
			!= Hero(Reinhardt)), Event Player, Knocked Down, Global.GLOBAL_ULT_DURATION);
	}
}